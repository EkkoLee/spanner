apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'ch.netzwerg.release'
apply plugin: 'checkstyle'

def currentVersion = file("${projectDir}/version.txt").text.trim();
def version = currentVersion

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {

    // Should be provided by the application, and only required for Spanner to work with unit test frameworks.
    provided 'com.android.support:support-annotations:23.0.1'
    provided 'com.android.support.test:runner:0.4.1'
    provided 'com.android.support.test:rules:0.4.1'
    provided 'junit:junit:4.12'

    // Required dependencies
    compile 'com.google.guava:guava:18.0'
    compile 'org.threeten:threetenbp:1.3.1'
    compile 'com.google.code.gson:gson:2.4'
    compile 'org.apache.commons:commons-math:2.2'
    compile 'com.sun.jersey:jersey-client:1.11'
}

def commonPom = {
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    issueManagement {
        system 'github'
        url 'https://github.com/cmelchior/spanner/issues'
    }
    scm {
        url 'scm:https://github.com/cmelchior/spanner'
        connection 'scm:git@github.com:cmelchior/spanner.git'
        developerConnection 'scm:git@github.com:cmelchior/spanner.git'
    }
}

publishing {
    publications {
        library(MavenPublication) {
            artifactId = 'spanner'
            groupId = 'dk.ilios'
            artifact file("${rootDir}/spanner/build/outputs/aar/spanner-release.aar")
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                Node root = asNode()
                root.appendNode('name', 'spanner')
                root.appendNode('description', 'Spanner is a micro benchmarking library for Android.')
                root.appendNode('url', 'https://github.com/cmelchior/spanner')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
                root.children().last() + commonPom
            }
        }
    }
}

artifactory {
    contextUrl = 'https://oss.jfrog.org/artifactory'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = project.hasProperty('spannerBintrayUser') ? spannerBintrayUser : 'noUser'
            password = project.hasProperty('spannerBintrayUser') ? spannerBintrayUser : 'noKey'
        }
        defaults {
            publications ('library')
        }
    }
}

bintray {
    user = project.hasProperty('spannerBintrayUser') ? spannerBintrayUser : 'noUser'
    key = project.hasProperty('spannerBintrayUser') ? spannerBintrayUser : 'noKey'

    dryRun = true
    publish = false

    publications = ['library']
    pkg {
        repo = 'maven'
        name = 'spanner'
        desc = 'Spanner - Microbencmarks on Android'
        websiteUrl = 'https://github.com/cmelchior/spanner'
        issueTrackerUrl = 'https://github.com/cmelchior/spanner/issues'
        vcsUrl = 'https://github.com/cmelchior/spanner.git'
        licenses = ['Apache-2.0']
        labels = ['aar', 'android', 'spanner', 'benchmark']
        publicDownloadNumbers = false
    }
}

// Configure ch.netzwerg.release plugin
// See https://github.com/netzwerg/gradle-release-plugin
release {
    dependsOn build
    push = true
    versionSuffix = '-SNAPSHOT'
    tagPrefix = 'v'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

task checkstyle(type: Checkstyle) {
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/R.java'
    exclude '**/BuildConfig.java'

//    def configProps = ['proj.module.dir': projectDir.absolutePath]
//    configProperties configProps

    // empty classpath
    classpath = files()
}
